// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:

    /// x pre settings
    
    /// zx setting
    Mux16(a=x, b=false, sel=zx, out=zx-x);
    /// nx setting
    Not16(in=zx-x, out=neg-zx-x);
    Mux16(a=zx-x, b=neg-zx-x, sel=nx, out=nx-zx-x);


    /// y pre setting

    /// zy setting
    Mux16(a=y, b=false, sel=zy, out=zy-y);
    /// ny setting
    Not16(in=zy-y, out=neg-zy-y);
    Mux16(a=zy-y, b=neg-zy-y, sel=ny, out=ny-zy-y);



    /// f function to compute
    And16(a=nx-zx-x, b=ny-zy-y, out=and-x-y);
    Add16(a=nx-zx-x, b=ny-zy-y, out=add-x-y);
    Mux16(a=and-x-y, b=add-x-y, sel=f, out=f-x-y);



    /// no
    Not16(in=f-x-y, out=no-f-x-y);
    Mux16(a=f-x-y, b=no-f-x-y, sel=no, out=out, out[0..7]=temp-out0, out[8..15]=temp-out1, out[15]=signed-bit);



    /// zr
    Or8Way(in=temp-out0, out=temp0);
    Or8Way(in=temp-out1, out=temp1);
    Or(a=temp0, b=temp1, out=temp2);
    Not(in=temp2, out=zr);



    /// ng
    Mux(a=false, b=true, sel=signed-bit, out=ng);
}